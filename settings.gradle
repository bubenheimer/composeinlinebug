pluginManagement {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
    }

    versionCatalogs {
        libs {
            version('android-plugin', '7.4.2')

            version('android-sdk-compile', 'android-33')
            version('android-sdk-min', '24')
            version('android-sdk-target', '33')

            version('androidx-activity', '1.7.0')
            version('androidx-compose-bom', '2023.03.00')
            version('androidx-compose-compiler', '1.4.4')

            version('java', JavaVersion.VERSION_11.toString())

            version('kotlin', '1.8.10')


            plugin('android-application', 'com.android.application')
                    .versionRef('android-plugin')

            plugin('kotlin-android', 'org.jetbrains.kotlin.android')
                    .versionRef('kotlin')


            library('androidx-activity-ktx', 'androidx.activity', 'activity-ktx')
                    .versionRef('androidx-activity')
            library('androidx-compose-activity', 'androidx.activity', 'activity-compose')
                    .versionRef('androidx-activity')
            library('androidx-compose-bom', 'androidx.compose', 'compose-bom')
                    .versionRef('androidx-compose-bom')
            library('androidx-compose-foundation', 'androidx.compose.foundation', 'foundation')
                    .withoutVersion()
            library('androidx-compose-runtime', 'androidx.compose.runtime', 'runtime')
                    .withoutVersion()
            library('androidx-compose-ui', 'androidx.compose.ui', 'ui')
                    .withoutVersion()
        }
    }
}

include(":app")
